apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'lib-opencc-android'

    publishedGroupId = 'com.zqc.opencc.android.lib'
    libraryName = 'lib-opencc-android'
    artifact = 'lib-opencc-android'
    libraryDescription = 'An Android library project for conversion between Traditional and Simplified Chinese'

    siteUrl = 'https://github.com/qichuan/android-opencc'
    gitUrl = 'https://github.com/qichuan/android-opencc.git'
    libraryVersion = '0.8.0'

    developerId = 'qichuan'
    developerName = 'Zhang Qichuan'
    developerEmail = 'qichuan@zhangqichuan.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


android {
    compileSdkVersion = 27
    buildToolsVersion = "27.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 27

        externalNativeBuild {
            ndkBuild {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64", "mips", "mips64"
                arguments "NDK_APPLICATION_MK:=src/main/jni/Application.mk"
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets
}

artifacts {
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}